buildscript {
	ext {
		snippetsDir = file('build/generated-snippets')
	}
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.5.3'
		classpath 'gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.22.2'
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.6.1'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'
apply plugin: 'org.asciidoctor.convert'


group = 'kkdad'
version = '0.0.1-SNAPSHOT'

dependencies {
	implementation 'com.google.code.gson:gson:2.8.7'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.google.guava:guava:30.1.1-jre'
	implementation 'org.jsoup:jsoup:1.14.1'

	implementation 'ch.qos.logback:logback-classic:1.2.5'
	implementation 'ch.qos.logback:logback-core:1.2.5'

	implementation(project(':ComicViewer'))

	implementation 'org.projectlombok:lombok:1.18.22'
	testImplementation 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.3.RELEASE'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
	testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

	testImplementation 'commons-io:commons-io:2.11.0'

	implementation 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.5.RELEASE'
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}


bootJar {
	baseName = 'comic-api'
	dependsOn asciidoctor, ':ComicViewer:buildComicViewer'
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
	from ("../ComicViewer/dist/ComicViewer") {
		into 'static'
	}
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

docker {
	name "${project.group}/${bootJar.baseName}"
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

bootRun  {
	main = "org.stapledon.api.ComicApiApplication"
}